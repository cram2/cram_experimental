# errorLevel/confirmation values
uint32 SUCCESS = 0
uint32 FAILED = 1
uint32 ERROR = 2
uint32 UNHANDLED_VALUE = 3
uint32 REMOVE_ALL_OBJECTS_FAILED = 4

# Actual request values
 
 # Realize operations on a world copy
  bool operationsWithCopy
  # Remove all objects before realizing any operation
  bool removeAllObjects
  # The operations
  ObjectOperation[] operations
  # Whether to update the camera's pose.
  bool updateCameraPose
  # The camera pose
  geometry_msgs/PoseStamped cameraPose
  # Simulate the world after application of all operations
  bool simulate
  # Realize simulation with a world copy. Has no effect if operationsWithCopy is set.
  bool simulateWithCopy
  # The duration the world will be simulated.
  duration duration
  # Whether to compute if each object is stable.
  bool computeStability
  # Whether to compute kitchen contact for each object before simulation.
  bool computeContactWithKitchenBeforeSimulation
  # Whether to compute kitchen contact for each object after simulation.
  bool computeContactWithKitchenAfterSimulation
  # Whether to compute collisions for each object before simulation.
  bool computeCollisionsBeforeSimulation
  # Whether to compute collisions for each object after simulation.
  bool computeCollisionsAfterSimulation
  # Whether to compute visibility for each object.
  bool computeVisibility
  # Whether to compute occlusions for each object.
  bool computeOcclusions

---

# Has there been an error, preventing the service call to finish?
uint32 errorLevel

bool computedStability
bool computedContactWithKitchenBeforeSimulation
bool computedContactWithKitchenAfterSimulation
bool computedCollisionsBeforeSimulation
bool computedCollisionsAfterSimulation
bool computedVisibility
bool computedOcclusions

# Information about all worlds that exist or have been created during this service call
WorldIntel[] worlds
